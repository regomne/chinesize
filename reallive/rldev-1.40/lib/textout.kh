{-# enc CP932 #-
  textout.kh: dynamically lineated text display engine.

  Copyright (c) 2006 Peter Jolly.

  This library is free software; you can redistribute it and/or modify it under
  the terms of the GNU Lesser General Public License as published by the Free
  Software Foundation; either version 2.1 of the License, or (at your option) any
  later version.

  This library is distributed in the hope that it will be useful, but WITHOUT
  ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
  FOR A PARTICULAR PURPOSE.  See the GNU Lesser General Public License for more
  details.

  You should have received a copy of the GNU Lesser General Public License
  along with this library; if not, write to the Free Software Foundation, Inc.,
  51 Franklin Street, Fifth Floor, Boston, MA  02110-1301  USA

  As a special exception to the GNU Lesser General Public License, you may
  include a publicly distributed version of the library in a "work that uses the
  Library" to produce a scenario file containing portions of the library, and
  distribute that scenario file under terms of your choice, without any of the
  additional requirements listed in clause 6 of the GNU Lesser General Public
  License.  A "publicly distributed version of the library" means either an
  unmodified library as distributed by Haeleth, or a modified version of the
  library that is distributed under the conditions defined in clause 2 of the
  GNU Lesser General Public License.  Note that this exception does not
  invalidate any other reasons why the scenario file might be covered by the
  GNU Lesser General Public License.

 Åyíçà”ÅzÇ±ÇÃÉRÅ[ÉhÇå©ÇÈêlÇÕã∂Ç§â¬î\ê´Ç™çÇÇ¢ÅBì«Ç›ÇÕé©å»ê”îCÇ…ÇƒÅB

  This file is spaghetti.  You should not attempt to read it.  If you want to
  know what happens here, consider donating large sums of money to me so I can
  afford to spend more time implementing proper structured programming in rlc
  and rewrite this in a language that can express this stuff readably.

  If you really have to read it anyway, do so in conjunction with src/textout.ml,
  as that contains part of the implementation.

  Note that this file contains many unsafe behaviours.  Remember to test in debug
  mode, with assertions enabled, before shipping any programs that make use of it.
-}

#ifdef __RLBABEL_KH__
  #error 'The Textout and rlBabel libraries are mutually exclusive: you can only use one or the other.'
#endif

#ifndef __TEXTOUT_KH__
#define __TEXTOUT_KH__

#ifndef __DynamicLineation__   #define __DynamicLineation__   #endif
#if !__DynamicLineation__      #set __DynamicLineation__ = 1  #endif
#ifdef __dynamic_textout_print #undef __dynamic_textout_print #endif

//-- Library initialisation --

#const __dto_windowcount = 0,
       __dto_any_have_r_command_mod = 0,
       __dto_has_neq = 0, // used locally to indicate if any non-contiguous windows exist
       __dto_default_window_id = 0 // I _think_ this is always the value, but I may be wrong.

//#define __dto_page_on_overflow // That will cause problems with mutating `tokens'.

// Determine the number of windows defined, and whether they're contiguous.
#for i = 0 .. 63
  if in_gameexe? ('WINDOW.\i:3{i}.MOJI_CNT')
    #set __dto_has_neq |= __dto_windowcount != i,
         __dto_windowcount += 1,
         __dto_any_have_r_command_mod |= gameexe ('WINDOW.\i:3{i}.R_COMMAND_MOD', 0, 0)

// Allocate variables

#const __dto_tokenspace = __int_alloc_space__
#if default (__DebugTextout__, 0) && !defined? (__NoDebug__)
  #define __AllLabelled__
#endif
int (block)
    __dto_currwidth  [__dto_windowcount],
    __dto_currindent [__dto_windowcount],
    __dto_currpos_y  [__dto_windowcount]
int __dto_fontsize   [__dto_windowcount],
    __dto_index,
    __dto_tokentype,
    __dto_arrayptr,
    __dto_tokenwidth, 
    __dto_tokenmode, 
    __dto_currwind,
    __dto_temp[3]
int (block)
    __dto_fontdefsz,
    __dto_fontxmod,
    __dto_fontymod,
    __dto_width,
    __dto_height,
    __dto_windflags
str (block)
    __dto_accum_str[4],
    __dto_token
str __dto_text
#if default (__DebugTextout__, 0) && !defined? (__NoDebug__)
  #undef __AllLabelled__
#endif


// Build a macro `__window_id_to_window_index' mapping window numbers to indices in an array.
#if __dto_has_neq
  // For the non-contiguous case, the function cannot be generated directly - it must
  // be built up as a string and passed back to the compiler to parse again. -}
  #const con_equivs = '', rev_equivs = '', var_equivs = '', idx = 0
  #for i = 0 .. 63
    if in_gameexe? ('WINDOW.\i:3{i}.MOJI_CNT')
      #set con_equivs += 'if id == \i{i} #redef __retval = \i{idx} #else',
           rev_equivs += 'if idx == \i{idx} #redef idx = \i{i} #else',
           var_equivs += 'of \i{i}, __retval = \i{idx} break ',
           idx += 1
  rlc_parse_string ("
    #inline __window_id_to_window_index (id):
      #sdefine __retval = store
      #if constant? (id)
        #" + con_equivs + "
          #error at (__CALLER_FILE__, __CALLER_LINE__, '#WINDOW.\\i:3{id} selected, but it is not defined')
        #endif
      #else
        case id
        " + var_equivs + "
        other
          __DebugMsgBox ('Error at \\s{__CALLER_FILE__} line \\i{__CALLER_LINE__}: #WINDOW.\\i:3{id} selected, but it was not defined at compile-time')
          __retval = 0
        ecase
      #endif
      return __retval;
    #inline __const_window_index_to_window_id (idx):
      assert (constant? (idx))
      #" + rev_equivs + " #endif
      return idx;
  ")                                                                                                                                                                                                                              //" to prevent UE32 syntax highlighting getting confused. X_x
  #undef con_equivs, rev_equivs, var_equivs, idx
#else
  // The contiguous case can be defined trivially.
  #inline __window_id_to_window_index (id):
    if id >= __dto_windowcount
      #if constant? (id)
        #error at (__CALLER_FILE__, __CALLER_LINE__, '#WINDOW.\i:3{id} selected, but it is not defined')
      #else
        __DebugMsgBox ('Error at \s{__CALLER_FILE__} line \i{__CALLER_LINE__}: #WINDOW.\i:3{id} selected, but it was not defined at compile-time')
      #endif
    return id;
  #inline __const_window_index_to_window_id (idx):
    assert (constant? (idx))
    return idx;
#endif
#undef __dto_has_neq

#inline __define_dto_variables ()
  {- This is a hack to permit reasonably readable code in the absence of any namespace system. -}
  #if 1 // scopeless block
    // Token types
    #const
      id_stext = 0,
      id_ntext = 1,
      id_dtext = 2,
      id_dquot = 3,
      id_space = 4,
      id_namev = 5,
      id_fsize = 6,
      id_vrble = 7,
      id_ccode = 8,
      id_wname = 9,
      id_ruby  = 10,
      id_emoji = 11,
      id_move  = 12

    // Output modes
    #const
      md_norm  = 0,
      md_name  = 1,
      md_ruby  = 2

    // Window flags
    #const
      wf_NAME_MOD      = $#1,
      wf_R_COMMAND_MOD = $#10,
      wf_INDENT_USE    = $#100

    // Constants
    #const
      fontmin = 8,
      fontmax = 64

    // Variables
    #sdefine
      tokenmode    = __dto_tokenmode,   // whether we're in a name block or not
      accum_str    = __dto_accum_str[0],// string: accumulator for name tokens and ruby glosses
      tokenspace   = __dto_tokenspace,  // index for __deref of space containing arrays
      text         = __dto_text,        // string: buffer for textual tokens
      arrayptr     = __dto_arrayptr,    // index in tokenspace of first element in token array (which is the array's length)
      token        = __dto_token,       // string: variable to hold current token
      tokenwidth   = __dto_tokenwidth,  // width of current or previous token
      tokentype    = __dto_tokentype,   // type of current token
      index        = __dto_index,       // index in token array of current token
      windowcount  = __dto_windowcount, // total number of defined windows (length of widthidx etc.)
      currwidth    = __dto_currwidth,   // array of current line widths (pixels) (index in tokenspace of first element)
      currindent   = __dto_currindent,  // array of current indent widths (pixels) (index in tokenspace of first element)
      curr_y       = __dto_currpos_y,   // array of current y positions (pixels) (index in tokenspace of first element)
      fontsz       = __dto_fontsize,    // array of current font sizes (pixels) (index in tokenspace of first element)
      cwind        = __dto_currwind,    // index of current window in widthidx etc.
      fontdefsz    = __dto_fontdefsz,   // default font size for current window (pixels)
      fontxmod     = __dto_fontxmod,    // extra character spacing (pixels)
      fontymod     = __dto_fontymod,    // extra leading (pixels)
      width        = __dto_width,       // maximum width of current window (pixels)
      height       = __dto_height,      // maximum height of current window (pixels)
      windflags    = __dto_windflags    // flags (OR-ed combination of wf_*)

    #sinline tokens(i) return __deref(tokenspace, arrayptr + i)
  #endif


//-- Wrappers --
#define __HideFunctionsSilently__

  // Wrapper utilities
  #inline __dto_pause_rcmod ():
    // clears currwidth[cwind] and currindent[cwind], inserts a line break
    setrng_stepped (currwidth[cwind], windowcount, 2)
    __dto_next_line;
  
  #inline __dto_reset_curr ()
    // clears currwidth[cwind], currindent[cwind], and curr_y[cwind]
    setrng_stepped (currwidth[cwind], windowcount, 3)

  #inline __dto_reset_all ()
    // clears currwidth[], currindent[], and curr_y[]
    setrng (currwidth[0], curr_y[windowcount - 1])

#inline msgClear ():
  at (__CALLER_FILE__, __CALLER_LINE__, 'msgClear')
  #if default (__DynamicLineation__, 1)
    __define_dto_variables __dto_reset_curr
  #endif;

#inline msgClearAll ():
  at (__CALLER_FILE__, __CALLER_LINE__, 'msgClearAll')
  #if default (__DynamicLineation__, 1)
    __define_dto_variables __dto_reset_all
  #endif;

#inline msgHide ():
  at (__CALLER_FILE__, __CALLER_LINE__, 'msgHide')
  #if default (__DynamicLineation__, 1)
    __define_dto_variables __dto_reset_curr
  #endif;

#inline msgHideAll ():
  at (__CALLER_FILE__, __CALLER_LINE__, 'msgHideAll')
  #if default (__DynamicLineation__, 1)
    __define_dto_variables __dto_reset_all
  #endif;

#inline br ():
  at (__CALLER_FILE__, __CALLER_LINE__, 'br')
  #if default (__DynamicLineation__, 1)
    __define_dto_variables
    __dto_next_line
  #endif;

#inline __dto_par (opcode):
  #undef __CALLER_FILE__, __CALLER_LINE__
  #if default (__DynamicLineation__, 1)
    __define_dto_variables
    currindent[cwind] = 0
    __dto_next_line
    #ifndef __dto_page_on_overflow
      // clear name to ensure indentation won't be restored unpredictably on screen overflows -_-;
      raw #8179 #817a endraw
    #endif
  #endif
  at (__CALLER_FILE__, __CALLER_LINE__, opcode)
  #define __CALLER_FILE__, __CALLER_LINE__;

#inline par ()
  __dto_par ('par')

#inline par2 ()
  __dto_par ('par2')

#inline pause ():
  #if default (__DynamicLineation__, 1)
    at (__CALLER_FILE__, __CALLER_LINE__, 'gosub @__dto_pause')
    #ifndef __dto_pause
      #inline __dto_pause():
        __define_dto_variables
        @__dto_pause
          #hiding pause, pause
          if __dto_any_have_r_command_mod && windflags & wf_R_COMMAND_MOD
            __dto_pause_rcmod
          else
            __dto_reset_all
          ret;
    #endif
  #else
    at (__CALLER_FILE__, __CALLER_LINE__, 'pause')
  #endif;

#inline pause_all ():
  at (__CALLER_FILE__, __CALLER_LINE__, 'pause_all')
  #if default (__DynamicLineation__, 1)
    __define_dto_variables
    // always leaves windows other than the current unchanged
    if __dto_any_have_r_command_mod && windflags & wf_R_COMMAND_MOD
      __dto_pause_rcmod
    else
      __dto_reset_curr
  #endif;

#inline page ():
  at (__CALLER_FILE__, __CALLER_LINE__, 'page')
  #if default (__DynamicLineation__, 1)
    __define_dto_variables
    // always closes windows other than the current?
    __dto_reset_all
  #endif;

#inline FontSize ([size]):
  __define_dto_variables
  #ifndef size
    #bind size = fontdefsz
    FontSize
  #else
    FontSize (size)
  #endif
  #if default (__DynamicLineation__, 1)
    fontsz[cwind] = constrain (fontmin, size, fontmax)
  #endif;

#inline FontSizeAll ([size]):
  __define_dto_variables
  #ifndef size
    #bind size = fontdefsz
    FontSizeAll
    #if default (__DynamicLineation__, 1)
      #const lst = ''
      #for i = 0 .. windowcount - 1
        #set lst += ', \i{gameexe ("WINDOW.\i:3{__const_window_index_to_window_id(i)}.MOJI_SIZE", 0, 8)}'
      rlc_parse_string ('setarray (fontsz[0]' + lst + ')')
    #endif
  #else
    FontSizeAll (size)
    #if default (__DynamicLineation__, 1)
      setrng (fontsz, size)
    #endif
  #endif
  #if default (__DynamicLineation__, 1)
    fontsz[cwind] = constrain (fontmin, size, fontmax)
  #endif;

#inline TextWindow ([style]):
  __define_dto_variables
  #ifndef style
    #bind style = __dto_default_window_id
    TextWindow
  #else
    TextWindow (style)
  #endif
  #if default (__DynamicLineation__, 1)
    cwind = at (__CALLER_FILE__, __CALLER_LINE__, __window_id_to_window_index (style))
    #for i = 0 .. windowcount - 1
      if style == i:
        #sinline key (k)
          return 'WINDOW.\i:3{__const_window_index_to_window_id(i)}.\s{k}'
        #const MOJI_SIZE = gameexe (key('MOJI_SIZE'), 0, 8)
        #const RUBY_SIZE = gameexe (key('LUBY_SIZE'), 0, 0)
        #const MOJI_REPX = gameexe (key('MOJI_REP'),  0, 0)
        #const MOJI_REPY = gameexe (key('MOJI_REP'),  1, 0)
        #const MOJI_CNTX = gameexe (key('MOJI_CNT'))
        #const MOJI_CNTY = gameexe (key('MOJI_CNT'), 1)
        
        {- A hack to solve a Kanon problem.  Kanon's windows are an odd number
           of hankaku characters wide, but RealLive doesn't allow for this.
           I can increase #WINDOW.000.MOJI_CNT's x value by one zenkaku character,
           and define `ADJUST_CNTX_000 = -1', to compensate. -}
        rlc_parse_string ("#ifdef ADJUST_CNTX_\i:3{__const_window_index_to_window_id(i)}, #set MOJI_CNTX += ADJUST_CNTX_\i:3{__const_window_index_to_window_id(i)} #endif")

        #const WIDTH = MOJI_CNTX * (MOJI_SIZE + MOJI_REPX)
        rlc_parse_string ("#ifdef WIDTH_MOD_\i:3{__const_window_index_to_window_id(i)}, #set WIDTH += WIDTH_MOD_\i:3{__const_window_index_to_window_id(i)} #endif")
        
        #const windflags_accum = 0
        #sinline sflag (wf, ky, def)
          #if gameexe (key(ky), 0, def) #set windflags_accum |= wf #endif
        #if !target_lt (1,2) // RealLive 1.1 and earlier don't have the INDENT_USE variable
          sflag (wf_INDENT_USE,  'INDENT_USE',    1)
        #endif
        sflag (wf_NAME_MOD,      'NAME_MOD',      0)
        sflag (wf_R_COMMAND_MOD, 'R_COMMAND_MOD', 0)
        setarray (fontdefsz,
          {-fontdefsz-} MOJI_SIZE,
          {-fontxmod-}  MOJI_REPX,
          {-fontymod-}  MOJI_REPY + RUBY_SIZE,
          {-width-}     WIDTH,
          {-height-}    MOJI_CNTY * (MOJI_SIZE + MOJI_REPY + RUBY_SIZE),
          {-windflags-} windflags_accum
        );
  #endif;

#inline __dto_set_x_offset ():
  currwidth[cwind] += __x
  if currwidth[cwind] >= width, __dto_next_line;

#inline __dto_set_y_offset ():
  curr_y[cwind] += __y;

#inline TextOffsetX (__x):
  __define_dto_variables
  #if !const_eq? (__x, 0)
    TextOffsetX (__x)
    __dto_set_x_offset
  #endif;

#inline TextOffsetY (__y):
  __define_dto_variables
  #if !const_eq? (__y, 0)
    TextOffsetY (__y)
    __dto_set_y_offset
  #endif;

#inline TextOffset (__x, [__y]):
  __define_dto_variables
  #ifndef __y
    TextOffsetX (__x)
  #elseif !const_eq? (__x, 0) || !const_eq? (__y, 0)
    TextOffset (__x, __y)
    #if !const_eq? (__x, 0)
      __dto_set_x_offset
    #endif
    #if !const_eq? (__y, 0)
      __dto_set_y_offset
    #endif
  #endif;

  #inline __dto_set_x ():
    currwidth[cwind] = __x
    if currwidth[cwind] >= width, __dto_next_line;

  #inline __dto_set_y ():
    curr_y[cwind] = __y;

#inline TextPosX (__x):
  __define_dto_variables
  TextPosX (__x)
  __dto_set_x;

#inline TextPosY (__y):
  __define_dto_variables
  TextPosY (__y)
  __dto_set_y;

#inline TextPos (__x, [__y]):
  __define_dto_variables
  #ifndef __y
    TextPosX (__x)
  #else
    TextPos (__x, __y)
    __dto_set_x
    __dto_set_y
  #endif;

#undef __HideFunctionsSilently__
// End of wrappers


//-- Dynamic textout functions. --

// These routines are private; they will be inserted automatically when needed.
// Do not call them directly from user code.

#inline __initialise_dynamic_textout ():
  // Ensure we always start with a known state.
  __define_dto_variables
  strclear (accum_str, token) // all temporary strings
  tokenmode = md_norm
  TextWindow
  FontSizeAll
  __dto_reset_all;

#inline __dto_next_line ():
  gosub @__dto_next_line;

#inline __dynamic_textout_print ():

  #ifdef __dto_pause __dto_pause #endif

  __define_dto_variables

  @__dto_next_line
    __dto_temp[2] = currwidth[cwind]
    currwidth[cwind] = currindent[cwind]
  @__dto_next_line2
GetTextPos(store, curr_y[cwind])
    curr_y[cwind] += fontsz[cwind] + fontymod
    if curr_y[cwind] + fontsz[cwind] + fontymod > height:
      __DebugMessage ('WARNING: screen overflow')
      #ifdef __dto_page_on_overflow
        if __dto_temp[2] > currindent[cwind], page
      #else
//                              Removed as it seems to cause problems. What was it for, again?
//      if __dto_temp[2] > currindent[cwind], spause // TEST: if this introduces a save point, it breaks the program.
        curr_y[cwind] = 0
        // Fix indent-related breakage if first character of next screen is ÅuÅwÅi
        strcpy(__dto_accum_str[2], token, 1)
        if strpos('ÅuÅwÅi', __dto_accum_str[2]) >= 0:
          __dto_accum_str[2] = ' '
          __dto_accum_str[2] += token
          token = __dto_accum_str[2]
          tokenwidth += (fontsz[cwind] + fontxmod) / 2;
      #endif;
    ret

  @__dynamic_textout_print
    #sdefine tempvar = __dto_temp[0]
    index = 1
    repeat
      // Each iteration, retrieve the next token.
      tokentype = tokens(index) & 15
      __dynamic_textout_gettoken
      // How we handle it depends on the mode we're in.
      case tokenmode
        // Tokens within \{this}, \ruby{}={this}, or \g{this}={}
        of md_name
        of md_ruby
          accum_str += token
          break

        // Normal tokens
        other
          #if default (__DebugTextout__, 0) && !defined? (__NoDebug__)
            __dto_accum_str[2] = '\i{tokentype} {\s{token}}'
            __DebugMessage (__dto_accum_str[2])
          #endif
          
          // Calculate character widths
          #sdefine zenkaku_char = __dto_temp[1], hankaku_char = __dto_temp[2]
          zenkaku_char = fontsz[cwind] + fontxmod
          hankaku_char = zenkaku_char / 2
          
          // If it is not empty...
          if strlen (token) > 0:
            // Special case for \e{} and \em{}, where length is always one zenkaku character.
            if tokentype == id_emoji:
              tempvar = 1 // zenkaku characters
              store = 1;  // characters
            else:            
              // Get numbers of full and half-width characters.  `store' holds `strlen (token)`.
              tempvar = store
              tempvar -= strcharlen (token);
            // Get width of token in pixels.  `store' holds `strcharlen (token)`.
            tokenwidth = tempvar * zenkaku_char + (store - tempvar) * hankaku_char
            
            // Check whether a line break is required.
            if currwidth[cwind] + tokenwidth >= width:
            
              // Never add line breaks for DBCS strings.
              if tokentype == id_dtext:
                // TODO: check that all this works!
                // `store' still holds `strcharlen (token)`.
                #sdefine
                  remaining_space = (width - currwidth[cwind]) / zenkaku_char,
                  chars_on_next_line = (store - remaining_space) % ((width - currindent[cwind]) / zenkaku_char)
                currwidth[cwind] = currindent[cwind] + chars_on_next_line * zenkaku_char
                __dto_temp[2] = 0
                gosub @__dto_next_line2
                goto @__dynamic_print_strout_token;
                
              // For SBCS strings, try trimming trailing spaces and see if that helps.
              else if tokentype == id_stext && tokenwidth + currwidth[cwind] - hankaku_char < width:
                // If it does, just increase the width counter.
                strtrunc (token, strcharlen (token) - 1)
                tokenwidth -= hankaku_char;
              else:
                #ifdef RELAXED_PUNCTUATION
                  // As a special case, we can ignore strings only one character too long when the last
                  // is a kinsoku character.  (For now, only certain common English kinsoku characters
                  // are supported.)
                  //#const ëSäpã÷ë• = "ÅfÅháÅÅvÅxÅzÅjÅnÅpÅrÅtÅEÅdÅcÅ[Å`ÅBÅAÅCÅDÅIÅHÅFÅGÇüÇ°Ç£Ç•ÇßÇ¡Ç·Ç„ÇÂÇÏÉ@ÉBÉDÉFÉHÉïÉñÉbÉÉÉÖÉáÉé"
                  //#const îºäpã÷ë• = "'\"£)]}>•∞°§,.!?:;ß®©™´Ø¨≠Æﬁﬂ"                  
                  if (tokentype == id_ntext && tokenwidth + currwidth[cwind] - hankaku_char < width)
                  || (tokentype == id_stext && tokenwidth + currwidth[cwind] - hankaku_char * 2 < width):     
                    #sdefine tstr = __dto_accum_str[2]
                    if tokentype == id_stext, tstr = strrsub (token, 2, 1) else tstr = strrsub (token, 1)
                    __dto_accum_str[1] = zentohan('.,:;!?)]}\'Åh')
                    tempvar = strpos (__dto_accum_str[1], tstr)
                    if tempvar >= 0:
                      if tokentype == id_stext, strtrunc (token, strcharlen (token) - 1)
                      currwidth[cwind] = width
                      goto @__dynamic_print_strout_token;;
                #endif
                // If the token is entirely whitespace, delete it altogether.
                if tokentype == id_space:
                  tokenwidth = 0
                  strclear (token);
                // Add a line break (the br() wrapper resets all relevant counters in the process)
                br;;
                
            currwidth[cwind] += tokenwidth
            // Print the token.
            @__dynamic_print_strout_token
            strout (token);
      ecase
      index += 1
    till index > tokens(0)
    ret;

#inline __dynamic_textout_gettoken ():
  #sinline exit () goto @__dynamic_textout_gettoken_exit
  #sinline merge (): goto_unless (tokenmode == md_name) @__dynamic_textout_gettoken_merge_sbcs, exit;
  #sdefine arg1 = __dto_temp[0], arg2 = __dto_temp[1]
  #sdefine nexttkn = tokens(index + 1) & 15
  arg1 = tokens(index) >> 12
  arg2 = tokens(index) >> 4 & $ff
  case tokentype
    // Text followed by space
    of id_stext
      token = strsub (text, arg1, arg2)
      token += ' '
      exit

    // DBCS text
    of id_dtext
      token = strsub (text, arg1, arg2)
      if tokenmode == md_norm && currwidth[cwind] == 0:
        accum_str = strsub (token, 0, 1)
        if strpos ('ÅuÅwÅi', accum_str) >= 0:
          if __dto_any_have_r_command_mod && windflags & wf_R_COMMAND_MOD, __dto_next_line
          currindent[cwind] += fontsz[cwind] + fontxmod;
      ;
      // Merge opening quotes with following text
      strpos('ÅuÅwÅi', token)
      goto_unless (tokenmode != md_name && store >= 0
        && index < tokens(0) && (nexttkn == id_stext || nexttkn == id_ntext)) @__dynamic_textout_gettoken_exit
      tokentype = id_ntext
      goto @__dynamic_textout_gettoken_merge_sbcs

    // Text not followed by space
    of id_ntext
      token = strsub (text, arg1, arg2)
      merge

    // Double quote
    of id_dquot
      token = zentohan ('Åh')
      tokentype = id_ntext
      goto_unless (tokenmode == md_norm) @__dynamic_textout_gettoken_exit

    @__dynamic_textout_gettoken_merge_sbcs
      #sdefine tstr = accum_str
      while index < tokens(0)
         && (nexttkn == id_stext || nexttkn == id_ntext || nexttkn == id_dquot || nexttkn == id_space):
        // Break if last char is hyphen
        __dto_accum_str[2] = strrsub (token, 1)
        if __dto_accum_str[2] == '-', exit
        // Handle case where next token is space
        if nexttkn == id_space:
          arg1 = tokens(index + 1) >> 12
          arg2 = tokens(index + 1) >> 4 & $ff
          assert (arg1 > 0)
          arg1 -= 1
          if arg1 > 0:
            #sdefine lhs = __deref(tokenspace, arrayptr + index + 1) // tokens(index + 1)
            lhs = id_space | arg1 << 12 | arg2 << 4;
          else
            index += 1
          tokentype = id_stext
          token += ' '
          exit;
        // Append next token
        index += 1
        if tokens(index) & 15 == id_dquot:
          tstr = zentohan ('Åh')
          token += tstr;
        else:
          arg1 = tokens(index) >> 12
          arg2 = tokens(index) >> 4 & $ff
          tstr = strsub (text, arg1, arg2)
          token += tstr
          if tokens(index) & 15 == id_stext:
            tokentype = id_stext
            token += ' '
            exit;;;
      exit

    // Just spaces
    of id_space
      // We create a string of N spaces in just two operations by formatting a number padded to 
      // N+1 digits with spaces and then truncating the digit away.
      token = itoa_s (0, arg1 + 1)
      strtrunc (token, arg1)
      exit

    // Name variable
    of id_namev
      if arg2 & $#01
        token = GetLocalName (arg1)
      else
        token = GetName (arg1)
      if arg2 & $#10:
        index += 1
        token = strsub (token, tokens(index), 1);
      // If this is an SBCS name in normal mode, we want to merge it with what follows.
      // Check last two characters - if both are SBCS, treat it as such.
      __dto_accum_str[2] = strrsub (token, 2)
      goto_unless (tokenmode == md_norm && strlen (__dto_accum_str[2]) == 2) @__dynamic_textout_gettoken_exit
      goto @__dynamic_textout_gettoken_merge_sbcs

    // äGï∂éö
    of id_emoji
      // FIXME: this is probably broken w.r.t. returning the correct token width.
      assert (tokenmode == md_norm, 'found emoji outside normal text')
      if arg2
        token = 'ÅîÇ`'
      else
        token = 'ÅîÇa'
      tstr = itoa_w (arg1, 2)
      token += tstr
      exit

    // Font size
    of id_fsize
      strclear (token)
      // Call our FontSize wrapper to set it
      if arg2
        FontSize (arg1)
      else
        FontSize
      exit

    of id_move
      strclear (token)
      if arg1 & $#01:
        index += 1
        store = tokens(index);
      if arg1 & $#10:
        index += 1
        arg1 = tokens(index);
      else
        arg1 = 0
      // Call the appropriate wrapper to set it
      if arg2 == 0
        TextOffset (store, arg1)
      else
        TextPos (store, arg1)
      exit

    // Inline variable
    of id_vrble
      #bind temp_str = __dto_accum_str[1], temp_str2 = __dto_accum_str[2], var_str = __dto_accum_str[3]
      strclear (token)
      case arg2
        of $0c
          var_str = strM[arg1]
          break
        of $12
          var_str = strS[arg1]
          break
        other
          rlcError ('invalid arg2 for id_vrble in __dynamic_textout_gettoken')
          exit
      ecase
      // Check - and compensate - for ÅñÇ` etc.
      while strused (var_str):
        // Get the next character from var_str.
        strcpy (temp_str2, var_str, 1)
        var_str = strsub (var_str, 1)
        // If it's not Åñ or Åì, loop.
        if strpos ('ÅñÅì', temp_str2) == -1, goto @__dto_name_glomp_loop_continue
        // If it is, peek ahead to see whether the next character forms a valid name
        // variable reference; loop if not.
        strcpy (temp_str, var_str, 1)
        arg1 = strcmp (temp_str, 'Ç`')
        if arg1 < 0 || temp_str > 'Çy', goto @__dto_name_glomp_loop_continue
        // If it is, get an index from it.
        arg2 = 0
        gosub @__dto_index_of_zenkaku_capital
        var_str = strsub (var_str, 1)
        // Set temp_str to the next character; if it's in Ç`..Çy, increase the index
        // appropriately, chomp it, and set temp_str to the character after.
        strcpy (temp_str, var_str, 1)
        arg1 = strcmp (temp_str, 'Ç`')
        if arg1 >= 0 && temp_str <= 'Çy':
          arg2 = (arg2 + 1) * 26
          gosub @__dto_index_of_zenkaku_capital
          var_str = strsub (var_str, 1)
          strcpy (temp_str, var_str, 1);
        // Retrieve the name variable.
        if temp_str2 == 'Åñ'
          temp_str2 = GetName (arg2)
        else
          temp_str2 = GetLocalName (arg2)
        // temp_str contains either an unrelated character or the index in the name.
        // If the latter, chomp another character from var_str and extract that indexed.
        arg1 = strcmp (temp_str, 'ÇO')
        if arg1 >= 0 && temp_str <= 'ÇX':
          var_str = strsub (var_str, 1)
          temp_str2 = strsub (temp_str2, atoi (temp_str), 1);
        // And loop, since temp_str2 now contains the correct text.
        @__dto_name_glomp_loop_continue
        token += temp_str2;
      merge

      @__dto_index_of_zenkaku_capital
        // Increase arg2 by the index of temp_str.
        arg2 += strpos ('Ç`ÇaÇbÇcÇdÇeÇfÇgÇhÇiÇjÇkÇlÇmÇnÇoÇpÇqÇrÇsÇtÇuÇvÇwÇxÇy', temp_str)
        ret

    // \{...}
    of id_wname
      if arg1 == 0:
        assert (tokenmode == md_norm, 'found name block opening outside normal text')
        tokenmode = md_name
        strclear (accum_str);
      else:
        assert (tokenmode == md_name, 'found name block closing outside name block')
        tokenmode = md_norm
        // Set the current name.
        store = __addr(__dto_accum_str[0]) & $ffff
        raw #8179 #232323 ?PRINT(strS[store]) #817a endraw
        // Increase the current width and set the indent level, when appropriate.
        if !(windflags & wf_NAME_MOD):
          arg1 = strlen (accum_str)
          arg1 -= strcharlen (accum_str) // store = strcharlen (accum_str)
          #sdefine name_width = arg1
          name_width = arg1 * (fontsz[cwind] + fontxmod) + (store - arg1) * (fontsz[cwind] / 2 + fontxmod / 2)
          if name_width + currwidth[cwind] >= width:
            // We could handle this gracefully, but let's be frank, it's a degenerate case.
            rlcError ('name block overflows window width.');
          currwidth[cwind] += name_width
          if arg2:
            // Arg2 is 0 when the next character is known to beÅuÅwÅi, so we can
            // assume this is regular text and make wrapping a bit prettier.
            raw #20 endraw
            #hiding TextOffsetX, TextOffsetX (-fontsz[cwind] - fontxmod) // hide our wrapper, we handle things here
            currwidth[cwind] += fontsz[cwind] / 2 + fontxmod / 2;
          if windflags & wf_INDENT_USE:
            currindent[cwind] += name_width + (fontsz[cwind] + fontxmod);
        ;;
      strclear (token)
      exit

    // Control codes
    of id_ccode
      strclear (token)
      case arg2
        of $0a // \n
          br
          exit

        of $0d // \r
          par
          exit

        of $16 // \p
          spause
          exit

        of $69 // \i:width{value} - width in arg1, next token is value
          index += 1
          token = itoa (tokens(index), arg1)
          tokentype = id_ntext
          merge

        of $63 // \c{[fg[, bg]]} - arg1 contains argc | (fg << 2) | (bg << 10).
          strclear (token)
          case arg1 & $#11
          of 0
            FontColour
            exit
          of 1
            FontColour (arg1 >> 2)
            exit
          other
            FontColour (arg1 >> 2 & $ff, arg1 >> 10)
            exit
          ecase

        of $77 // \wait{time}
          strclear (token)
          wait (arg1)
          exit

        other
          rlcError ('undefined control code in __dynamic_textout_gettoken')
      ecase
      exit

    // Ç”ÇËÇ™Ç»
    of id_ruby
      strclear (token)
      case arg2
        // \ruby
        of 0 // \ruby{
          assert (tokenmode == md_norm, 'found ruby block opening outside normal text')
          #hiding __doruby, __doruby // hide the blocking wrapper in rlapi
          exit
        of 1 // }={
          assert (tokenmode == md_norm, 'found ruby gloss opening outside normal text')
          tokenmode = md_ruby
          strclear (accum_str)
          exit
        of 2 // }
          assert (tokenmode == md_ruby, 'found ruby gloss closing outside ruby gloss')
          tokenmode = md_norm
          #hiding __doruby, __doruby (accum_str) // hide the blocking wrapper in rlapi
          exit
      ecase

    {- TODO: handle other cases -}
    // Report an error for unknown token types.
    other
      rlcError ('undefined token type in __dynamic_textout_gettoken')
      strclear (token)
  ecase
  @__dynamic_textout_gettoken_exit;

#endif // !defined? (__TEXTOUT_KH__)
